prefix conll: <http://ufal.mff.cuni.cz/conll2009-st/task-description.html#>
prefix powla: <http://purl.org/powla/powla.owl#>
prefix nif: <http://persistence.uni-leipzig.org/nlp2rdf/ontologies/nif-core#>
PREFIX tiger: <https://www.ims.uni-stuttgart.de/documents/ressourcen/werkzeuge/tigersearch/doc/html/TigerXML.html#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX apf: <http://jena.apache.org/ARQ/property#>

# transform CoNLL-RDF data structures to tiger-compliant data structures
# warning: custom Apache Jena function!
# note: we generate multiple labels here
DELETE {
  ?n rdf:value ?value; a conll:PARSE
} INSERT {
  ?n a tiger:Nonterminal; tiger:cat ?cat.
  ?r a powla:Relation; powla:hasSource ?n; powla:hasTarget ?p; tiger:label ?feat.
} WHERE {
  ?n a conll:PARSE.
  OPTIONAL {
    ?n rdf:value ?value.
    BIND(replace(?value,'-.*','') as ?cat)
  }
  OPTIONAL {
    ?n powla:hasParent ?p.
    ?n a powla:Node; rdf:value ?value.
    ?feat apf:strSplit (?value "-").
    FILTER(?feat!=?cat)
    BIND(?feat as ?label)
    BIND(uri(concat(str(?n),"_",replace(str(?p),'.*[#/]',''))) as ?r)
  }
};

INSERT {
  ?parent ?rel ?dep
} WHERE {
  [] powla:hasSource ?dep; powla:hasTarget ?parent; tiger:label ?label.
  BIND(URI(concat("https://www.ims.uni-stuttgart.de/documents/ressourcen/werkzeuge/tigersearch/doc/html/TigerXML.html#",
  encode_for_uri(?label))) as ?rel)
};
